# 11. 句法扩展和模块

本章描述了 Chez Scheme 对 `syntax-case` 句法抽象机制所做的扩展，这些扩展现已被 R^6RS 报告所标准化。这些扩展包括模块系统（11.5 节）、元定义（11.8 节）、可以作为`syntax-rules` 防护板的按情况展开（11.9 节）、`fluid-let-syntax` 以及 `include` 。


## 11.1 fluid 关键字绑定

通过 R^6RS 报告中的 `define-syntax`、`let-syntax` 以及 `letrec-syntax` 形式定义的关键字绑定，可以使用 `fluid-let-syntax` 临时地重新绑定。

+ 语法：`(fluid-let-syntax ((keyword expr) ...) form1 form2 ...) `
+ 返回：参考解释
+ 库：`(chezscheme)`

每一条 `expr` 必须求值为一个转换器。`fluid-let-syntax` 类似于标准的 `let-syntax`，只不过不会引入关键字 `keyword ...` 的新绑定，`fluid-let-syntax` 会在展开它的程序体的时候，临时修改关键字已有的绑定。也就是说，在展开 `form1 form2 ...` 的时候，每个关键字可见的（或最高层）词法绑定被临时地替换为一个新的关联——即一个关键字与其对应的转换器。这会影响对解析为相同词法（或顶级）绑定的关键字的任何引用，无论引用是在出现在程序体正文还是在展开时引入。相反，`let-syntax` 仅捕获出现在程序体正文中的那些引用。

> #TBD, DeathKing  
> body 要如何翻译？  
> + 显然不是“语句体”，因为 Scheme 中没有语句（statement）这个说法；  
> + 显然也不是“过程体”，因为这里是待展开的形式；  
> + 这里没有使用 `<body>` 这个语法记号，因此也不适宜保留 `<body>` 这样的形式；  
> + 暂时使用“程序体”的说法，如果有更好的说法，稍后会修改。

下面的例子展示了 `fluid-let-syntax` 和 `let-syntax` 的不同：

```scheme
(let ([f (lambda (x) (+ x 1))])
  (let-syntax ([g (syntax-rules ()
                    [(_ x) (f x)])])
    (let-syntax ([f (syntax-rules ()
                      [(_ x) x])])
      (g 1)))) ; => 2 

(let ([f (lambda (x) (+ x 1))])
  (let-syntax ([g (syntax-rules ()
                    [(_ x) (f x)])])
    (fluid-let-syntax ([f (syntax-rules ()
                            [(_ x) x])])
      (g 1)))) ; => 1
```

除了在第二条表达式中，`let-syntax` 形式内部表达式中第一条表达式是 `fluid-let-syntax` 形式，这两条表达式都是相同的。在第一条表达式中，出现在展开 `(g 1)` 过程中的 `f` 引用的是由 `let` 式所绑定的变量 `f`， 而在第二条表达式中的 `f` 引用的是由 `fluid-let-syntax` 所绑定的关键字 `f`。

下面的例子利用 `fluid-let-syntax` 来定义 `define-integrable` 形式，后者类似于用于过程定义的 `define`，但无论在何处直接调用该过程，都导致过程的代码被集成或插入到该位置 #TBD。通过 `define-integrable` 或 `define` 来定义的过程没有语义差异，只不过在最高层， `define-integrable` 形式必须在首次引用所定义的标识符之前出现。词法作用域被保持了，集成调用中的实际参数只在适当的时间求值一次，可集成过程可以用作第一类值，并且递归过程不会导致无限递归扩展。

```scheme
(define-syntax define-integrable
  (syntax-rules (lambda)
    [(_ name (lambda formals form1 form2 ...))
     (begin
       (define xname
         (fluid-let-syntax ([name (identifier-syntax xname)])
           (lambda formals form1 form2 ...)))
       (define-syntax name
         (lambda (x)
           (syntax-case x ()
             [_ (identifier? x) #'xname]
             [(_ arg (... ...))
              #'((fluid-let-syntax ([name (identifier-syntax xname)])
                   (lambda formals form1 form2 ...))
                  arg
                  (... ...))]))))]))
```

`define-integrable` 的语法如下：

```
(define-integrable name lambda-expression)
```

`define-integrable` 形式会被展开为一个由定义构成的序对：关于 `name` 的句法定义以及关于 `xname` 的变量定义 #TBD。跟 `name` 对应的转换器将对于 `name` 的调用展开为对 `lambda`-表达式的直接调用。由于展开的结果仅仅是直接的 `lambda` 应用（等价于 `let` 表达式），因此在求值过程的体之前，实际参数只会刚好被求值一次，正如所要求的那样。所有指向 `name` 的引用都会被指向 `xname` 的引用所替代。`xname` 的定义将其绑定到 `lambda`表达式的值上。这使得该过程可以被用作第一类值。由于 `xname` 是由转换器引入的，因此除了由 `name` 对应的转换器引入的对其的引用，`xname` 的绑定在其它任何地方都是不可见的。

无论 `lambda`-表达式出现在何处，在其内部 `name` 都被重新绑定到一个转换器上，将所有的 `name` 展开为对 `xname` 的引用。以这样的目的使用 `fluid-let-syntax` 可以防止在可集成过程中间接递归的无限展开。 这允许过程可以递归调用而不会导致无限扩展。 `define-integrable` 没有特意维护词法作用域，这是因为词法作用域由展开器自动维护。

Chez Scheme 在适当时自动集成本地定义的过程。但是，它无法集成最高层定义的过程，因为可以随时（通过eval或load）将为最高层变量的赋值代码引入系统。即使本地绑定过程的集成留给编译器，`define-integrable` 可用于强制集成在最高层的定义的过程绑定。它还可以用于强制编译器通常不会集成的大型过程。 （扩展/优化过程对于确定何时是否进行集成非常有用。）

## 11.2 `syntax-rules` 转换器

Chez Scheme 扩展了 `syntax-rules` 以便允许子句中使用 `syntax-case` 子句中的 fender #TBD。

+ 语法：`(syntax-rules (literal ...) clause ...) `
+ 返回值：转换器 
+ 库：`(chezscheme)`

`literal` 必须是除下划线(`_`)、省略号（`...`）以外的标识符。子句必须是下列形式之一：

```scheme
(pattern template)
(pattern fender template)
```

R6RS只支持第一种形式。

## 11.3. `syntax-case` 转换器

Chez Scheme 提供了几种可用于简化某些句法抽象编码的过程和句法形式。

+ 过程：`(syntax->list syntax-object)`
+ 返回值：一个句法对象表
+ 库：`(chezscheme)`

此过程接受表示表结构形式的句法对象，并返回由句法对象组成的表，其中每个句法对象与输入形式的子形式相对应。

`syntax->list` 可以按如下定义：

```scheme
(define syntax->list
  (lambda (ls)
    (syntax-case ls ()
      [() '()]
      [(x . r) (cons #'x (syntax->list #'r))]))) 

#'(a b c) ; =>  #<syntax (a b c)>
(syntax->list #'(a b c)) ; => (#<syntax a> #<syntax b> #<syntax c>)
```

对于从单个模式变量值或模式变量值序列构造的表，不需要调用 `syntax->list`，因为这样的结构已经是表。例如：

```scheme
(list? (with-syntax ([x #'a] [y #'b] [z #'c]) #'(x y z)))) ; => #t
(list? (with-syntax ([(x ...) #'(a b c)]) #'(x ...))) ; =>  #t
```

+ 过程： `(syntax->vector syntax-object) `
+ 返回值： 一个句法对象表
+ 库：`(chezscheme)`

此过程接受表示向量结构形式的句法对象，并返回由句法对象组成的向量，其中每个句法对象与输入形式的子形式相对应。

> 原句为：This procedure takes a syntax object representing a vector-structured form and returns a **list** of syntax objects, each representing the corresponding subform of the input form.  
> 从例子来看，返回的是一个向量（vector），而不是一个表（list），疑有误。 #TBD

`syntax->vector` 可以按如下定义：

```scheme
(define syntax->vector
  (lambda (v)
    (syntax-case v ()
      [#(x ...) (apply vector (syntax->list #'(x ...)))]))) 

#'#(a b c) ; => #<syntax #(a b c)>
(syntax->vector #'#(a b c)) ; => #(#<syntax a> #<syntax b> #<syntax c>)
```

对于从单个模式变量值或模式变量值序列构造的向量，不需要调用 `syntax->vector`，因为这样的结构已经是向量。例如：

```scheme
(vector? (with-syntax ([x #'a] [y #'b] [z #'c]) #'#(x y z)))) ; => #t
(vector? (with-syntax ([(x ...) #'(a b c)]) #'#(x ...))) ; => #t
```

+ 过程： `(syntax-object->datum obj)`
+ 返回值： 一个剔除了句法信息的对象
+ 库：`(chezscheme)`

`syntax-object->datum` 跟 R6RS 中的 `syntax->datum` 完全相同。

+ 过程： `(datum template)`
+ 返回值：见如下
+ 库：`(chezscheme)`

`(datum template)` 是一种方便的简写语法，表示：

```scheme
(syntax->datum (syntax template))
```

可以像下面这样简单地定义 `datum`：

```scheme
(define-syntax datum
  (syntax-rules ()
    [(_ t) (syntax->datum #'t)])) 

(with-syntax ((a #'(a b c))) (datum a)) ; => (a b c)
```

+ 过程：`(datum->syntax-object template-identifier obj)`
+ 返回值：一个语法对象
+ 库：`(chezscheme)`

`datum->syntax-object` 跟 R6RS 中的 `datum->syntax` 完全相同。

+ 句法：`(with-implicit (id0 id1 ...) body1 body2 ...)`
+ 返回值：见如下
+ 库：`(chezscheme)`

这个句法抽象了用于创建隐式标识符的 `datum->syntax` 的常见用法（参见上文）。句法：

```scheme
(with-implicit (id0 id1 ...)
  body1 body2 ...)
```

等同于：

```scheme
(with-syntax ([id1 (datum->syntax #'id0 'id1)] ...)
  body1 body2 ...)
```

可以像下面这样简单地定义 `with-implicit`：

```scheme
(define-syntax with-implicit
  (syntax-rules ()
    [(_ (tid id ...) b1 b2 ...)
     (with-syntax ([id (datum->syntax #'tid 'id)] ...)
       b1 b2 ...)]))
```

我们用 `with-implicit` 来简化上述的 `loop` 实现（这也是正确的版本）。

```scheme
(define-syntax loop
  (lambda (x)
    (syntax-case x ()
      [(k e ...)
       (with-implicit (k break)
         #'(call-with-current-continuation
             (lambda (break)
               (let f () e ... (f)))))])))
```

+ 句法：`(include path)`
+ 返回值：未定义
+ 库：`(chezscheme)`

`path`必须是一个字符串。`include` 形式被展开为一个 `begin` 表达式，后者包含了在名为 `path` 的文件中找到的形式。例如，如果文件 `f-def.ss` 包含了 `(define f (lambda () x))`，那么表达式：

```scheme
(let ([x "okay"])
  (include "f-def.ss")
  (f))
```

会被求值为 `"okay"`。如果 `include` 形式出现在定义序列中，并且由 `path` 指定的文件中的形式都是定义，则将其视为定义，如上例所示。如果文件包含表达式，则 `include` 形式将被视为表达式。

尽管 Chez Scheme 使用依赖于实现的定义，允许它捕获和维护包含代码的源信息，但是可移植版本的 `include` 可以按如下定义：

```scheme
(define-syntax include
  (lambda (x)
    (define read-file
      (lambda (fn k)
        (let ([p (open-input-file fn)])
          (let f ([x (read p)])
            (if (eof-object? x)
                (begin (close-input-port p) '())
                (cons (datum->syntax k x)
                      (f (read p))))))))
    (syntax-case x ()
      [(k filename)
       (let ([fn (datum filename)])
         (with-syntax ([(exp ...) (read-file fn #'k)])
           #'(begin exp ...)))])))
```

`include` 的定义使用 `datum->syntax` 将从文件读取的对象转换为适当词法上下文中的句法对象，以便这些表达式中的标识符引用和定义在 `include` 形式出现的范围内。

在 Chez Scheme 的 `include` 实现中，参数 `source-directories`（第12.5节）决定搜索未由绝对路径名标识的源文件的目录集。

+ 过程：`(syntax-error obj string ...)`
+ 返回值：不返回
+ 库：`(chezscheme)`

可以使用 `syntax-error` 报告句法错误，该句法错误通过串联 `string ...` 和 `obj` 的打印表示来生成消息。如果未提供字符串参数，则使用字符串 `"invalid syntax"`。 当 `obj` 是句法对象时，在创建打印表示之前，会剥离句法对象包装器（与`syntax->datum` 一样）。如果源文件信息存在于 `syntax-object` 包装器中，则 `syntax-error` 将此信息合并到错误消息中。

如果输入无法匹配其中一个子句，则 `syntax-case` 和 `syntax-rules` 会自动调用 `syntax-error` 。

我们可以使用 `syntax-error` 来精确地报告在（匿名）`let` 以下定义的中检测到的错误的诱因。

```scheme
(define-syntax let
  (lambda (x)
    (define check-ids!
      (lambda (ls)
        (unless (null? ls)
          (unless (identifier? (car ls))
            (syntax-error (car ls) "let cannot bind non-identifier"))
          (check-ids! (cdr ls)))))
    (define check-unique!
      (lambda (ls)
        (unless (null? ls)
          (let ([x (car ls)])
            (when (let mem? ([ls (cdr ls)])
                    (and (not (null? ls))
                         (or (bound-identifier=? x (car ls))
                             (mem? (cdr ls)))))
              (syntax-error x "let cannot bind two occurrences of")))
          (check-unique! (cdr ls)))))
    (syntax-case x ()
      [(_ ((i e) ...) b1 b2 ...)
       (begin
         (check-ids! #'(i ...))
         (check-unique! #'(i ...))
         #'((lambda (i ...) b1 b2 ...) e ...))])))
```

有了这些变动，下面的表达式：

```
(let ([a 3] [a 4]) (+ a a))
```

会产生这样的错误消息：`"let cannot bind two occurrences of a."`。

+ 过程：`(literal-identifier=? identifier1 identifier2)`
+ 返回值：见下
+ 库：`(chezscheme)`

这个过程同 R6RS 中的 `identifier=?` 一致，提供此函数只是为了保证向后兼容。

## 11.4. 编译期值与编译期属性

定义一系列互相依赖的宏时，在展开器用来记录有关变量、关键字、模块名称等信息的同一个编译环境中，将信息附加在标识符上通常会带来极大的方便。例如，一个用于定义记录类型的宏，如 `define-record-type`，可能需要在编译期环境中将信息附加到该记录类型的名称中，以用于处理子记录类型的定义。

Chez Scheme提供了两种机制，用于在编译期环境中将信息附加到标识符上：编译期值和编译期属性。编译期值是一种转换器，可以通过 `define-syntax` ，`let-syntax` ， `letrec-syntax` 和 `fluid-let-syntax` 与标识符相关联。 当标识符与编译期值相关联时，它也不具有任何其他含义，并且尝试将其作为普通标识符引用会导致语法错误。 另一方面，编译期属性与现有绑定一起维护，提供有关绑定的其他信息。 当发生对标识符的普通引用时，将忽略属性。

宏用于获取编译期值和属性的机制是类似的。 在这两种情况下，宏的转换器都返回一个过程 `p` 而非语法对象。扩展器使用一个参数调用 `p` ，这是一个环境查找过程 `lookup`，然后 `p` 可以在构造宏的最终输出之前使用它来获取一个或多个标识符的编译期值和属性。 `lookup` 接受一个或两个标识符参数。 当使用一个参数 `id` 时，`lookup` 返回 `id`的编译期值，如果 `id` 没有编译期值，则返回`#f`。 使用 `id` 和 `key` 两个参数时，`lookup` 返回 `id` 的 `key` 属性值，如果 `id` 没有 `key` 属性，则返回 `#f`。

+ 过程： `(make-compile-time-value obj)` 
+ 返回值：一个编译期值 
+ 库：`(chezscheme)`

编译期值是一种转换器，使得一个关键字可以通过任何关键字绑定结构与之关联，例如 `define-syntax` 或 `let-syntax` 。转换器封装了提供的对象。可以如上所述检索封装的对象。

以下示例说明了如何使用此功能来定义简单的语法记录定义机制，其中记录类型描述符是在扩展时生成的。


```scheme
(define-syntax drt
  (lambda (x)
    (define construct-name
      (lambda (template-identifier . args)
        (datum->syntax template-identifier
          (string->symbol
            (apply string-append
              (map (lambda (x)
                     (if (string? x)
                         x
                         (symbol->string (syntax->datum x))))
                   args))))))
    (define do-drt
      (lambda (rname fname* prtd)
        (with-syntax ([rname rname]
                      [rtd (make-record-type-descriptor
                             (syntax->datum rname) prtd #f #f #f
                             (list->vector
                               (map (lambda (fname)
                                      `(immutable ,(syntax->datum fname)))
                                    fname*)))]
                      [make-rname (construct-name rname "make-" rname)]
                      [rname? (construct-name rname rname "?")]
                      [(rname-fname ...)
                       (map (lambda (fname)
                              (construct-name fname rname "-" fname))
                            fname*)]
                      [(i ...) (enumerate fname*)])
          #'(begin
              (define-syntax rname (make-compile-time-value 'rtd))
              (define rcd (make-record-constructor-descriptor 'rtd #f #f))
              (define make-rname (record-constructor rcd))
              (define rname? (record-predicate 'rtd))
              (define rname-fname (record-accessor 'rtd i))
              ...))))
    (syntax-case x (parent)
      [(_ rname (fname ...))
       (for-all identifier? #'(rname fname ...))
       (do-drt #'rname #'(fname ...) #f)]
      [(_ rname pname (fname ...))
       (for-all identifier? #'(rname pname fname ...))
       (lambda (lookup)
         (let ([prtd (lookup #'pname)])
           (unless (record-type-descriptor? prtd)
             (syntax-error #'pname "unrecognized parent record type"))
           (do-drt #'rname #'(fname ...) prtd)))])))

(drt prec (x y))
(drt crec prec (z))
(define r (make-crec 1 2 3))
(prec? r) ; => #t
(prec-x r) ; => 1
(crec-z r) ; => 3
prec ; => exception: invalid syntax prec
```

+ 句法：`(define-property id key expr)`
+ 返回值：未定义 
+ 库：`(chezscheme)`

`define-property` 形式将属性附加到现有标识符绑定，而不会干扰该绑定范围内标识符的现有含义。它通常由一个宏用于记录有关绑定的信息以供另一个宏使用。 `id` 和 `key` 都必须是标识符。表达式 `expr` 会在 `define-property` 形式展开时被计算，并将 `expr` 值关联的新属性以 `key` 为键附加到 `id` 的现有绑定，该绑定必须具有可见的本地或最高层绑定。

`define-property` 是一个定义，可以出现在定义可以出现的其他任何地方。`define-property` 引入的属性的范围是 `define-property` 形式所出现的整个体，如果它出现在最高层，则是全局的，除非它由相同 `id` 和 `key` 的属性替换，或者它所附着的标识符的绑定被遮蔽（shadowed）了。可以使用不同的键将任意数量的属性附加到同一个绑定。将与已附加的属性同名的新属性附加到绑定上，会使新属性遮蔽现有属性。

下面的示例定义了一个宏 `get-info`，它检索绑定的 `info` 属性，并定义变量 `x`，将`info` 属性附加到 `x` 的绑定，通过 `get-info` 检索属性，引用 `x` 以显示其正常绑定仍然完好无损，并在 `x` 的不同绑定范围内再次使用 `get-info` ，以显示绑定与编译期值同时被外部的 `x` 遮蔽。

```scheme
(define info)
(define-syntax get-info
  (lambda (x)
    (lambda (lookup)
      (syntax-case x ()
        [(_ q)
         (let ([info-value (lookup #'q #'info)])
           #`'#,(datum->syntax #'* info-value))]))))
(define x "x-value")
(define-property x info "x-info")
(get-info x) ; => "x-info"
x ; => "x-value"
(let ([x "inner-x-value"]) (get-info x)) ; => #f
```

调试时，在给定标识符和键的情况下，拥有一个检索任意属性的句法形式通常很有用。下面的 `get-property` 宏就是这样。

```scheme
(define-syntax get-property
  (lambda (x)
    (lambda (r)
      (syntax-case x ()
        [(_ id key)
         #`'#,(datum->syntax #'* (r #'id #'key))]))))
(get-property x info) <graphic> "x-info"
```

在使用 `get-property` 的情况下，两个标识符的绑定都必须可见。

下面定义的 `drt` 版本类似于使用上面的 `make-compile-time-value` 定义的版本，只不过它将记录名称定义为一个宏，并且使用更具描述性的消息引发异常，同时将记录类型描述符作为一个单独的属性附加到绑定上。与 `drt` 一起定义的变量 `drt-key` 仅用作 `drt` 附加到记录名称的属性的键。`drt-key` 和 `drt` 都在一个只导出后者的模块中定义，确保 `drt` 使用的属性不能被访问或伪造。

```scheme
(library (drt) (export drt) (import (chezscheme))
  (define drt-key)
  (define-syntax drt
    (lambda (x)
      (define construct-name
        (lambda (template-identifier . args)
          (datum->syntax template-identifier
            (string->symbol
              (apply string-append
                (map (lambda (x)
                       (if (string? x)
                           x
                           (symbol->string (syntax->datum x))))
                     args))))))
      (define do-drt
        (lambda (rname fname* prtd)
          (with-syntax ([rname rname]
                        [rtd (make-record-type-descriptor
                               (syntax->datum rname) prtd #f #f #f
                               (list->vector
                                 (map (lambda (fname)
                                        `(immutable ,(syntax->datum fname)))
                                      fname*)))]
                        [make-rname (construct-name rname "make-" rname)]
                        [rname? (construct-name rname rname "?")]
                        [(rname-fname ...)
                         (map (lambda (fname)
                                (construct-name fname rname "-" fname))
                              fname*)]
                        [(i ...) (enumerate fname*)])
            #'(begin
                (define-syntax rname
                  (lambda (x)
                    (syntax-error x "invalid use of record name")))
                (define rcd (make-record-constructor-descriptor 'rtd #f #f))
                (define-property rname drt-key 'rtd)
                (define make-rname (record-constructor rcd))
                (define rname? (record-predicate 'rtd))
                (define rname-fname (record-accessor 'rtd i))
                ...))))
      (syntax-case x (parent)
        [(_ rname (fname ...))
         (for-all identifier? #'(rname fname ...))
         (do-drt #'rname #'(fname ...) #f)]
        [(_ rname pname (fname ...))
         (for-all identifier? #'(rname pname fname ...))
         (lambda (lookup)
           (let ([prtd (lookup #'pname #'drt-key)])
             (unless prtd
               (syntax-error #'pname "unrecognized parent record type"))
             (do-drt #'rname #'(fname ...) prtd)))]))))

(import (drt))
(drt prec (x y))
(drt crec prec (z))
(define r (make-crec 1 2 3))
(prec? r) ; => #t
(prec-x r) ; => 1
(crec-z r) ; => 3
prec ; => exception: invalid use of record name prec
```

## 11.5 模块

模块用于帮助将程序组织成不同的部分，这些部分通过声明好的接口干净地交互。尽管模块化编程通常可以为多人开发大型程序的带来便利，但它也可以在 Chez Scheme 中以“微模块”级别使用，因为 Chez Scheme 中的模块和 `import` 句法形式属于定义并且可以出现在任何地方。可能会出现其他类型的定义，包括在 `lambda` 表达式的体内或其他局部作用域内。

模块控制绑定的可见性，可以视为扩展词法作用域，以允许更精确地控制绑定的可见范围。模块导出标识符的绑定，即变量的绑定、关键字的绑定或模块名称的绑定。模块可以是**具名的**或**匿名的**。只要模块的名称可见，那么在具名模块出现处导入的绑定都可见。匿名模块中导出的绑定，在模块出现的地方被隐式地导入。匿名模块可用于隐藏一组绑定，同时允许集合中的其余绑定可见。

本节中给出的一些文本和示例改编自“扩展句法抽象的作用域”[32]，后者更详细地描述了模块及其实现。

+ 句法：`(module name interface defn ... init ...)`
+ 句法：`(module interface defn ... init ...)`
+ 返回值：未定义
+ 库：`(chezscheme)`

`name` 是标识符，`defn ...` 是定义，`init ...` 是表达式。`interface` 是形如 `(export ...)` 的导出表，其中每个导出都是标识符标识符或者形式 `(identifier export ...)`。

模块的第一种语法建立一个命名范围，用来封装一组标识符的绑定。在模块名可见的任何地方，导出的绑定都可以通过 `import` 或 `import-only`（第10.4节）来可见。模块的第二种语法引入了一个匿名模块，其 `module` 形式出现时隐式导入绑定（就好像通过导入隐藏的模块名称一样）。#TBD

模块由一组（可能为空的）定义和一组（可能为空的）初始化表达式序列组成。模块中定义的标识符在模块的体中可见，被导出的标识符在模块的导入范围内亦可见。模块接口中列出的每个标识符必须在该模块中定义或被导入该到模块。模块形式是一个定义，因此可以出现在其他定义可以出现的任何位置，包括嵌套在 `lambda` 表达式的体中，嵌套在库和最顶层程序形式中，以及嵌套在其他模块中。此外，因为模块名的作用域与其他标识符相同，所以模块和库可以导出模块名称以及变量和关键字。

当接口包含形如 `(identifier export ...)` 的形式时，只有 `identifier` 在导入上下文中可见。 `export ...` 中的标识符是间接导入，就像通过 `indirect-export` 形式声明一样（第10.4节）。

模块名与其他标识符占用相同的命名空间，并遵循相同的作用域规则。除非被导出，否则模块中定义的标识符仅在该模块中可见。

模块内的表达式可以引用在模块外部绑定的标识符。

```scheme
(let ([x 3])
  (module m (plusx)
    (define plusx (lambda (y) (+ x y))))
  (import m)
  (let ([x 4])
    (plusx 5))) ; => 8
```

同样，除了那些被导入的标识符所遮蔽的变量，`import` 不会阻止对在该形式出现时可见的标识符的访问。

```scheme
(module m (y) (define y 'm-y))
(let ([x 'local-x] [y 'local-y])
  (import m)
  (list x y)) ; => (local-x m-y)
```

另一方面，在模块中使用 `import-only` 会建立一个隔离的作用域，其中唯一可见的是被导入模块所导出的标识符。

```scheme
(module m (y) (define y 'm-y))
(let ([x 'local-x] [y 'local-y])
  (import-only m)
  x) ; => Error: x is not visible
```

对静态验证来说，有时需要这个功能，以确保除了显式地被导入到模块或局部作用域的标识符之外，没有使用其他的标识符。

除非通过仅导入导出的模块导入或仅导入以及至少一个模块的名称，否则无法在仅导入表单范围内进行后续导入。 要创建包含多个模块的导出而不使导入或仅导入可见的隔离范围，要导入的所有模块必须以相同的仅导入形式列出。#TBD

另一种解决方案是创建一个包含每个其他模块的导出的模块。

```scheme
(module m2 (y) (define y 'y))
(module m1 (x) (define x 'x))
(module mega-module (cons x y)
  (import m1)
  (import m2)
  (import scheme))
(let ([y 3])
  (import-only mega-module)
  (cons x y)) ; => (x . y)
```

在编译之前，源程序被翻译成不含句法抽象，句法定义，库定义，模块定义以及 `import` 形式的核心语言程序。翻译由句法扩展器执行，语法扩展器以递归下降的方式处理源程序中的特殊形式。

`define-syntax` 特殊形式在翻译期环境中将关键字与转换器相关联。当展开器遇到关键字时，它会调用关联的转换器并重新处理生成的形式。`module` 特殊形式将模块名称与接口相关联。当扩展器遇到 `import` 特殊形式时，它会从翻译期环境中提取相应的模块接口，并使导出的绑定在导入表单出现的作用域中可见。

内部定义和模块语句体中的定义由左至右地进行处理，以便模块的定义和导入可以出现在相同的定义序列中。然而，只有在处理完整个定义集之后，才能在体内和变量定义的右侧出现表达式，从而允许变量和句法定义之间的完全相互递归。#TBD

模块和 `import` 特殊形式仅影响标识符在源程序中的可见性，而不影响其语义。特别地，无论变量被绑定到模块内部或外部定义的位置，导入都不会引入新位置。为了保持由模块和语法抽象建立的作用域关系，局部变量在必要时会被重命名。因此，表达式：

```scheme
(let ([x 1])
  (module m (x setter)
    (define-syntax x (identifier-syntax z))
    (define setter (lambda (x) (set! z x)))
    (define z 5))
  (let ([y x] [z 0])
    (import m)
    (setter 3)
    (+ x y z))) ; => 4
```

等价于下面的表达式，其中标识符被统一地更名，并带上了下标。

```scheme
(let ([x0 1])
  (define-syntax x1 (identifier-syntax z1))
  (define setter1 (lambda (x2) (set! z1 x2)))
  (define z1 5)
  (let ([y3 x0] [z3 0])
    (setter1 3)
    (+ x1 y3 z3)))
```

存在于 `lambda`、最顶层的 `begin`、最顶层的程序，`library` 或 `module` 的程序体中的定义，在展开期由展开器从左到右处理，变量定义在运行时从左到右进行计算。 在求值完变量定义之后，依次求值模块体内出现的初始化表达式。

可以通过多种方式定义互递归的模块。在以下程序中，`a` 和 `b` 是由匿名模块导出的相互递归模块，该匿名模块的局部作用域用于静态链接两者。 例如，模块 `a` 中的自由变量 `y` 指的是通过导入闭合模块中的 `b` 所提供的 `y` 的绑定。

```scheme
(module (a b)
  (module a (x) (define x (lambda () y)))
  (module b (y) (define y (lambda () x)))
  (import a)
  (import b))
```

以下语法抽象泛化了此模式以允许定义多个互递归的模块。

```scheme
(define-syntax rec-modules
  (syntax-rules (module)
    [(_ (module m (id ...) form ...) ...)
     (module (m ...)
       (module m (id ...) form ...) ...
       (import m) ...)]))
```

因为模块可以重新导出所导入的绑定，所以很容易在单个模块上提供多个视图，因为 `s` 和 `t ` 提供下面的 `r`，或者将几个模块组合成一个复合，就像 `r` 一样。#TBD


```scheme
(module p (x y)
  (define x 1) (define y 2))
(module q (y z)
  (define y 3) (define z 4))
(module r (a b c d)
  (import* p (a x) (b y))
  (import* q (c y) (d z)))
(module s (a c) (import r))
(module t (b d) (import r))
```

为了能够让接口与实现分离，以下语法抽象支持命名接口的定义和使用。

```scheme
(define-syntax define-interface
  (syntax-rules ()
    [(_ name (export ...))
     (define-syntax name
       (lambda (x)
         (syntax-case x ()
           [(_ n defs)
            (with-implicit (n export ...)
              #'(module n (export ...) .
                  defs))])))])) 

(define-syntax define-module
  (syntax-rules ()
    [(_ name interface defn ...)
     (interface name (defn ...))]))
```

`define-interface` 创建一个接口宏，给定模块名称和定义表，该接口宏将其展开为具有具体接口的模块定义。

`with-implicit` 用于确保引入的 `export` 标识符在与 `define-module` 特殊形式中模块名称相同的作用域中可见。#TBD

`define-interface` 和 `define-module` 可以按如下使用。

```scheme
(define-interface simple (a b))
(define-module m simple
  (define-syntax a (identifier-syntax 1))
  (define b (lambda () c))
  (define c 2))
(let () (import m) (+ a (b))) ; => 3
```

下面定义的抽象模块的功能允许在求值模块形式时逐步地满足模块接口。使得接口和实现之间可以灵活地分离、支持互递归模块的单独编译，并允许重新定义模块实现。

```scheme
(define-syntax abstract-module
  (syntax-rules ()
    [(_ name (ex ...) (kwd ...) defn ...)
     (module name (ex ... kwd ...)
       (declare ex) ...
       defn ...)])) 

(define-syntax implement
  (syntax-rules ()
    [(_ name form ...)
     (module () (import name) form ...)]))
```

在 `abstract-module` 特殊形式中，表 `ex ...` 中的每个导出必须是变量。 这些变量的值由一个或多个单独的 `implement` 形式提供。由于关键字绑定必须在编译期出现，因此不能逐步满足它们，而是将它们列为单独的导出并在抽象模块中定义。

Within an implement form, the sequence of forms form ... is a sequence of zero or more definitions followed by a sequence of zero or more expressions. Since the module used in the expansion of implement does not export anything, the definitions are all local to the implement form. The expressions may be arbitrary expressions, but should include one satisfy form for each variable whose definition is supplied by the implement form. A satisfy form has the syntax

在 `implement` 特殊形式中，形式序列 `form ...` 是由零个或多个定义组成的序列，后跟零个或多个表达式的序列。 由于用于展开 `implement` 的模块不会导出任何内容，因此对 `implement` 来说，这些定义都是局部的。表达式可以是任意表达式，但对于那些由 `implement` 特殊形式提供的变量定义，应该为每个变量包含一个 `satisfy` 形式。 `satisfy` 特殊形式的语法为：

```scheme
(satisfy variable expr)
```

`declare` 以及 `satisfy` 可以简单地等价于 `define` 以及 `set!`。

```scheme
(define-syntax declare (identifier-syntax define))
(define-syntax satisfy (identifier-syntax set!))
```

Alternatively, declare can initialize the declared variable to the value of a flag known only to declare and satisfy, and satisfy can verify that this flag is still present to insure that only one attempt to satisfy the value of a given identifier is made.

或者，`declare` 可以将声明的变量初始化一个只被 `decalre` 和 `satisfy` 所知道的标志值，并且 `satisfy` 可以验证该标志是否仍然存在，以确保满足给定标识符的值的尝试只发起过一次。

```scheme
(module ((declare cookie) (satisfy cookie))
  (define cookie "chocolate chip")
  (define-syntax declare
    (syntax-rules () [(_ var) (define var cookie)]))
  (define-syntax satisfy
    (syntax-rules ()
      [(_ var exp)
       (if (eq? var cookie)
           (set! var exp)
           (assertion-violationf 'satisfy
             "value of variable ~s has already been satisfied"
             'var))])))
```

利用 `abstract-module` 以及 `implement`，我们可以像下面这样定义互递归、可独立编译的模块：

```scheme
(abstract-module e (even?) (pred)
  (define-syntax pred
    (syntax-rules () [(_ exp) (- exp 1)]))) 

(abstract-module o (odd?) ()) 

(implement e
  (import o)
  (satisfy even?
    (lambda (x)
      (or (zero? x) (odd? (pred x)))))) 

(implement o
  (import e)
  (satisfy odd?
    (lambda (x) (not (even? x))))) 

(let () (import-only e) (even? 38)) ; => #t
```

+ 语法：`only` 
+ 语法：`except`
+ 语法：`add-prefix` 
+ 语法：`drop-prefix` 
+ 语法：`rename`
+ 语法：`alias`
+ 库：`(chezscheme)`

这些标识符都是 `import` 和 `import-only` 的辅助关键字。除非是在它们被识别为辅助关键字的上下文中，否则引用这些标识符是违反语法的。

## 11.6. 独立导入以及独立导出形式

第10.4节中描述的局部导入和导出形式可以在模块内和模块内使用。#TBD

