# 7. 对象操作

本章介绍了 Chez Scheme 中针对非数值对象的特定操作，包括 pairs, numbers 等标准对象，以及 boxes, records 等 Chez Scheme 扩展对象。第 8 章介绍了针对数字的操作。关于对象的标准操作的更多描述，参见“The Scheme Programming Language，第 4 版”的第 6 章，或 Scheme 第 6 修订版的报告。


### 7.1. R6RS 中缺少的类型判断

过程: (enum-set? obj)

返回: 如果 `obj` 为 `enum set` 则为 `#t`, 否则为 `#f`. 

库: (chezscheme)

此一谓词应当被定义，但缺失于第 6 修订版中。

过程: (record-constructor-descriptor? obj)

返回: 如果 `obj` 为 `record constructor` 则为 `#t`, 否则为 `#f`.

库: (chezscheme)

此一谓词应当被定义，但缺失于第 6 修订版中。


### 7.2. 点对和列表

过程: (atom? obj)

返回: 如果 `obj` 不是点对，则为 `#t`, 否则为 `#f`.

库: (chezscheme)

atom? 等价于 (lambda (x) (not (pair? x))).

    (atom? '(a b c)) => #f
    (atom? '(3 . 4)) => #f
    (atom? '()) => #t
    (atom? 3) => #t

过程: (list-head list n)

返回: 列表前 n 个元素的列表

库: (chezscheme)

n 须为一精确非负整数，且小于或等于列表的长度。

`list-head` 可以和另一个 Scheme 标准过程 `list-tail` 配合，用于将一个列表分割成两个单独的列表。不过， `list-tail`
并不进行空间分配，只是返回原列表的子列表，而 `list-head` 则总是返回原列表前面部分的一份拷贝。

`list-head` 可依如下定义：

    (define list-head
      (lambda (ls n)
        (if (= n 0)
            '()
            (cons (car ls) (list-head (cdr ls) (- n 1))))))
    
    (list-head '(a b c) 0) => ()
    (list-head '(a b c) 2) => (a b)
    (list-head '(a b c) 3) => (a b c)
    (list-head '(a b c . d) 2) => (a b)
    (list-head '(a b c . d) 3) => (a b c)
    (list-head '#1=(a . #1#) 5) => (a a a a a)

过程: (last-pair list)

返回: 列表的最后一个点对

库: (chezscheme)

列表不可为空。 `last-pair` 返回列表的最后一个点对（不是最后一个元素）。列表可以是一个不严格的列表（improper list），此时，最后一个点对为一个包含最末元素和结束对象的点对。

    (last-pair '(a b c d)) => (d)
    (last-pair '(a b c . d)) => (c . d)

过程: (list-copy list)

返回: 列表的一份拷贝

库: (chezscheme)

`list-copy` 返回与列表相等( `equal?` )的一个列表，使用新的点对重新构成顶层的列表结构。

    (list-copy '(a b c)) => (a b c)
    
    (let ([ls '(a b c)])
      (equal? ls (list-copy ls))) => #t
    
    (let ([ls '(a b c)])
      (let ([ls-copy (list-copy ls)])
        (or (eq? ls-copy ls)
            (eq? (cdr ls-copy) (cdr ls))
            (eq? (cddr ls-copy) (cddr ls))))) => #f

过程: (list\* obj &#x2026; final-obj)

返回: 由 `obj ...` 组成的列表，结束于 `final-obj`

库: (chezscheme)

`list*` 和第 6 修订版中的 `cons*` 是一样的。

过程: (make-list n)

过程: (make-list n obj)

返回: n 个对象的列表

库: (chezscheme)

n 须为非负整数。如果省略 obj，则列表的元素为未指定的。

    (make-list 0 '()) => ()
    (make-list 3 0) => (0 0 0)
    (make-list 2 "hi") => ("hi" "hi")

过程: (iota n)

返回: 从 0（包含）到 n（不包含）的整数列表

库: (chezscheme)

n 须为精确的非负整数。

    (iota 0) => ()
    (iota 5) => (0 1 2 3 4)

过程: (enumerate ls)

返回: 从 0（包含）到长度 ls（不包含）的整数列表。

库: (chezscheme)

    (enumerate '()) => ()
    (enumerate '(a b c)) => (0 1 2)
    (let ([ls '(a b c)])
      (map cons ls (enumerate ls))) => ((a . 0) (b . 1) (c . 2))

过程: (remq! obj list)

过程: (remv! obj list)

过程: (remove! obj list)

返回: 列表中所有 `obj` 都被移除后的列表

库: (chezscheme)

这些过程与第 6 修订版中的 `remq`, `remv`, 及 `remove` 过程类似，只是 `remq!`, `remv!` 和 `remove!` 使用输入列表中的点对来构成输出列表。它们进行较少的空间分配，但并不一定比它们非破坏性的相应版本更快。如果滥用，很容易导致混乱或错误的结果。

    (remq! 'a '(a b a c a d)) => (b c d)
    (remv! #\a '(#\a #\b #\c)) => (#\b #\c)
    (remove! '(c) '((a) (b) (c))) => ((a) (b))

过程: (substq new old tree)

过程: (substv new old tree)

过程: (subst new old tree)

过程: (substq! new old tree)

过程: (substv! new old tree)

过程: (subst! new old tree)

返回: old 被替换为 new 后的树

库: (chezscheme)

这些过程遍历树，以对象 new 替换树中所有与对象 old 相等的对象。

对于 `substq` 和 `substq!` ，相等性测试是基于 `eq?` , `substv` 和 `substv!` 是基于 `eqv?`, 而 `subst` 和 `subst!` 是基于 `equal?`.

`substq!`, `substv!`, 和 `subst!` 执行破坏性的替换。它们进行较少的空间分配，但并不一定比它们非破坏性的对应版本更快。如果滥用，很容易导致混乱或错误的结果。

    (substq 'a 'b '((b c) b a)) => ((a c) a a)
    
    (substv 2 1 '((1 . 2) (1 . 4) . 1)) => ((2 . 2) (2 . 4) . 2)
    
    (subst 'a
           '(a . b)
           '((a . b) (c a . b) . c)) => (a (c . a) . c)
    
    (let ([tr '((b c) b a)])
      (substq! 'a 'b tr)
      tr) => ((a c) a a)

过程: (reverse! list)

返回: 对原列表反向排序的列表

库: (chezscheme)

`reverse!` 通过反转其链接破坏性地反向排序列表。以 `reverse!` 取代 `reverse` 减少了空间分配，但并不一定比使用 `reverse` 更快。如果滥用，会很容易导致混乱或错误的结果。

    (reverse! '()) => ()
    (reverse! '(a b c)) => (c b a)
    
    (let ([x '(a b c)])
      (reverse! x)
      x) => (a)
    
    (let ([x '(a b c)])
      (set! x (reverse! x))
      x) => (c b a)

过程: (append! list &#x2026;)

返回: 输入列表的串联

库: (chezscheme)

如同 `append`, `append!` 返回一个新的列表，其中元素依次为第一个列表中的元素，第二个列表中的元素，第三个列表中的元素，等等。不同之处在于， `append!` 重用所有参数中的点对以构造新列表。即，每一个列表参数的最后一个 `cdr`, 其后一元素变为指向下一个列表参数。除最后一个参数外，如果任一参数为空列表，它实质上会被忽略。最后一个参数（并不一定得是列表）是不变的。

相比于 `append`, `append!` 进行更少的空间分配，但并不一定更快。如果滥用，会很容易导致混乱或错误的结果。

    (append! '(a b) '(c d)) => (a b c d)
    
    (let ([x '(a b)])
      (append! x '(c d))
      x) => (a b c d)
